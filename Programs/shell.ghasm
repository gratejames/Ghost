JMP PROG_START
#INC ./lib/kernel.ghasm
#INC ./lib/vfs.ghasm
#INC ./lib/ramdisk.ghasm
#INC ./lib/stringPrint.ghasm
#INC ./lib/charPrint.ghasm
#INC ./shellstrings.ghasm
PROG_START:

LD R0 RAMDISK
LD R1 $RAMDISKSIZE
CALL ramdiskInit

ST 0b0000000000000001 $0xaff0	;; Background
ST 0b0000011111001000 $0xaff1	;; Forground

ST KeyboardInterrupt $0xaf00	;; Register Keyboard Interrupt
ST 3 $0xafef					;; Display mode

ST printString $0xaf5e			;; Register printString as the string printing interrupt
ST printChar $0xaf5f			;; Register printChar as the character printing interrupt

;; Char Print Routine (5f)
; 0xaf70: Character
; 0xaf71: X Pos
; 0xaf72: Y Pos
; 0xaf73: Font Address

CALL RefreshDisplay
loop:
	CALL blinkCursor
	CALL delay
	JMP loop
HLT

delay:
	LD R0 0
	delayLoop:
		INC R0
		CNE R0 0x200
		JMPC delayLoop
	RET

blinkCursor:
	LD R0 $Timer
	; DBG R0
	INC R0
	ST R0 $Timer
	CE R0 0
	JMPC showCursor
	CE R0 0x8000
	JMPC hideCursor
	RET

	hideCursor:
		LD R1 $Cursor
		ADD R1 CharMem
		DD R1
		LDD R0
		CALL SetAtCursor
		RET

	showCursor:
		LD R0 '_' 			; Character
		CALL SetAtCursor
		RET

KeyboardInterrupt:
	IDIS
	PSH
	ST 0xffff $Timer
	LD R1 $0xafd0					; Get key state
	CE R1 0
	LD R0 $0xafd1					; Load the new character
	JMPC KI_Keyup

	CE R0 0x0008 ; Backspace
	JMPC KI_Backspace
	CE R0 0x000d ; Enter
	JMPC KI_Enter
	CE R0 0x00e3 ; L_Win
	JMPC KI_Win_Down
	CE R0 0x00e2 ; L_Alt
	JMPC KI_Alt_Down
	CE R0 0x00e1 ; L_Shift
	JMPC KI_Shift_Down
	CE R0 0x00e0 ; L_Control
	JMPC KI_Control_Down
	
	; DBG R0
	CALL ApplyShift

	LD R1 $Cursor
	ADD R1 CharMem
	DD R1
	STD R0

	CALL PrintAtCursor

	KeyboardInterruptExit:
	POP
	IEN
	RET

	KI_Keyup:
		CE R0 0x00e3 ; L_Win
		JMPC KI_Win_Up
		CE R0 0x00e2 ; L_Alt
		JMPC KI_Alt_Up
		CE R0 0x00e1 ; L_Shift
		JMPC KI_Shift_Up
		CE R0 0x00e0 ; L_Control
		JMPC KI_Control_Up
		JMP KeyboardInterruptExit
		
	KI_Win_Up:
		LD R0 0
		LD R1 3
		CALL SetModifier
		JMP KeyboardInterruptExit
	KI_Win_Down:
		LD R0 1
		LD R1 3
		CALL SetModifier
		JMP KeyboardInterruptExit
		
	KI_Alt_Up:
		LD R0 0
		LD R1 2
		CALL SetModifier
		JMP KeyboardInterruptExit
	KI_Alt_Down:
		LD R0 1
		LD R1 2
		CALL SetModifier
		JMP KeyboardInterruptExit

	KI_Shift_Up:
		LD R0 0
		LD R1 0
		CALL SetModifier
		JMP KeyboardInterruptExit
	KI_Shift_Down:
		LD R0 1
		LD R1 0
		CALL SetModifier
		JMP KeyboardInterruptExit
		
	KI_Control_Up:
		LD R0 0
		LD R1 1
		CALL SetModifier
		JMP KeyboardInterruptExit
	KI_Control_Down:
		LD R0 1
		LD R1 1
		CALL SetModifier
		JMP KeyboardInterruptExit

	KI_Backspace:
		LD R1 $Cursor
		CLT R1 $LastCursor
		JMPC KeyboardInterruptExit
		CE R1 $LastCursor
		JMPC KeyboardInterruptExit

		LD R0 ' '
		CALL SetAtCursor
		LD R1 $Cursor
		DEC R1
		ST R1 $Cursor
		ADD R1 CharMem
		DD R1
		STD ' '
		LD R0 ' '
		CALL SetAtCursor
		JMP KeyboardInterruptExit

	KI_Enter:
		LD R1 $Cursor		; Load the cursor position
		ADD R1 CharMem
		DD R1
		STD 0				; Draw a space in charmem
		LD R0 ' '
		CALL SetAtCursor	; Write a null terminator to the text memory

		MV $Cursor $CommandEnd

		CALL CursorNewline

		CALL handleUserInput

		LD R1 $Cursor
		ADD R1 CharMem
		DD R1
		STD '>'
		LD R0 '>'
		CALL SetAtCursor	; Write and draw the prompt character
		LD R1 $Cursor		; Leave the cursor after the prompt
		INC R1
		ST R1 $Cursor

		MV $Cursor $LastCursor

		JMP KeyboardInterruptExit

; No arguments, free to destroy all
handleUserInput:
	LD R1 $LastCursor
	ADD R1 CharMem

	DD R1
	LDD R2
	CEZ R2
	RETC
	; String Index into table of routine pointers
	
	LD R2 HUI_Table
	HUI_LookupLoop:
		DD R1
		LDD R0
		DD R2
		LDD R3

		; If the character in the input is a space, replace it with a 0 in the register states so that commands match even with arguments
		CNE R0 ' '
		JMPC HUI_NotASpace
		LD R0 0
		HUI_NotASpace:
		
		; R0 is character
		; R1 is character address in user input
		; R2 is the pointer to the string to compare to
		; R3 is the table character
		; DBGC R0
		; DBGC R3
		PSH R0
			XOR R3 ; R0 XOR R3 => R0 (Checks if R0 and R3 are equal)
			CEZ R0
		POP R0
		JMPC HUI_Match
		HUI_NotMatch:
			; DBGC 'N'
			; DBGC 0x0a
			; This string is a miss, we need to scan to the next one, skip over the pointer
			; and prepare for the next search
			HUI_NotMatchSkip:
				CNZ R3
				INC R2
				DD R2
				LDD R3
				JMPC HUI_NotMatchSkip ; Scan through the table for the next zero

			ADD R2 2 ; Since it was a miss, skip the pointer and the next zero
			; Now, before we go back, lets reset the user input pointer as well
			LD R1 $LastCursor
			ADD R1 CharMem
			; Better luck next time!

			DD R2
			LDD R3
			CNZ R3
			JMPC HUI_LookupLoop
			; to check against is zero, then we've exhausted the table
			JMP HUI_Exhausted ; If the first character in the string

		HUI_Match:
			; DBGC '!'
			; DBGC 0x0a
			; The current characters match. Scan to the next ones. If the current ones are both zeroes
			; (We've reached the end of both strings) then we need to escape the loop and declare a match
			INC R2
			INC R1
			CNZ R0
			JMPC HUI_LookupLoop

	; MATCH!!! Both strings have ended.
	; DBGC 'M'

	LD R0 $CommandEnd
	SUB R0 $LastCursor

	DD R2 ; Load the pointer to the function
	LDD R3
	DD R3
	; Commands are run so that
	; R0 is the length of the input
	; R1 is a pointer to the arguments (Starting on the first char after the space)
	CALD ; Call that pointer

	; DBGC 'R'
	; DBGC 0x0a
	RET
	HUI_Exhausted:
		LD R1 STR_CMD_NOT_FOUND
		CALL PrintLineAtCursor
		RET

; Destroys R1
CursorNewline:
	LD R1 $Cursor		; Load the cursor position
	ADD R1 0x0040		; Reset the cursor to the next line
	AND R1 0xffc0
	ST R1 $Cursor
	RET

currentDirectory:
.ds "/"
.dz (12+1)*8 +1

absoluteDirectory:
.ds "/"
.dz (12+1)*8 +1

currentDisk: .db 0

CMD_CD:
	SUB R0 2
	CLT R0 1
	JMPC CMD_CD_NOARG
	CALL MakeAbsoluteDir
	; R1 has pointer to whole path
	PSH
	LD R0 $currentDisk ; Disk
	CALL vfs_list
	CNZ R0
	JMPC CMD_CD_BADPATH
	LDZ R3
	CALL free
	POP

	LD R2 currentDirectory

	CMD_CD_LOOP:
		DD R1
		LDD R0
		DD R2
		STD R0
		INC R1
		INC R2
		CNZ R0
		JMPC CMD_CD_LOOP
	RET
	CMD_CD_NOARG:
	LD R1 STR_CD_ERROR_NOARG
	CALL PrintLineAtCursor
	RET

	CMD_CD_BADPATH:
		; DBG R0
		POP
		LD R1 STR_CD_ERROR_BADPATH
		CALL PrintLineAtCursor
		RET

CMD_ECHO:
	CALL PrintLineAtCursor
	RET

CMD_PWD:
	LD R1 currentDirectory
	CALL PrintLineAtCursor
	RET


CMD_LS:
	LD R0 $currentDisk ; Disk
	LD R1 currentDirectory ; Ptr to path
	CALL vfs_list

	CNZ R0
	JMPC PRINT_CMD_LS_ERROR

	PSH R3

	LDZ R3
	LD R1 0
	LD R3 0
	; DBGC '|'
	VFSLIST_debug:
		DD R0
		LDD R2
		CEZ R2
		JMPC VFSLIST_debug_zero
		; DBGC R2
			PSH
			LDZ R2
			CALL PrintAtCursor
			POP
		JMP VFSLIST_debug_nonzero
		VFSLIST_debug_zero:
		CEZ R3
		JMPC VFSLIST_debug_done
		VFSLIST_debug_nonzero:
		INC R0
		INC R1
		INC R3
		CNE R3 12
		JMPC VFSLIST_debug_cont
		LD R3 0
		; DBGC '|'
		; DBGC 0x0a
		; DBGC '|'
			PSH
			LD R0 ' '
			CALL PrintAtCursor
			POP
		VFSLIST_debug_cont:
		CLT R1 96
		JMPC VFSLIST_debug

	VFSLIST_debug_done:
	; DBGC '|'
	PSH R1
	CALL CursorNewline
	POP R1
	POP R0
	CALL free
	RET

	PRINT_CMD_LS_ERROR:
		PSH R1
		CE R0 0x0001
		JMPC CMD_LS_ERROR_1
		LD R1 STR_LS_ERROR

		PRINT_CMD_LS_ERROR_EXIT:
			CALL PrintLineAtCursor
			POP R1
			RET

		CMD_LS_ERROR_1:
			LD R1 STR_LS_ERROR_1
			JMP PRINT_CMD_LS_ERROR_EXIT

CMD_MKDIR:
	; R1 has pointer to 0 terminated string
	; Need to convert to an 12 char padded string
	SUB R0 5
	CLT R0 1
	JMPC CMD_MKDIR_NOARG
	CALL MakeAbsoluteDir
	CALL SplitDirectory
	PSH R2

	; PSH
	; CALL PrintLineAtCursor
	; POP

	; R1 has pointer to path
	; R2 has pointer to file name
	; Stack has pointer to file name to free it

	LD R0 $currentDisk ; Disk
	; LD R1 currentDirectory ; Ptr to path
	; R2 is pointer to file name
	CALL vfs_mkdir
	CNZ R0
	CALLC PRINT_CMD_MKDIR_ERROR

	POP R0
	CALL free
	RET

CMD_MKDIR_NOARG:
	LD R1 STR_MKDIR_NOARG
	CALL PrintLineAtCursor
	RET

PRINT_CMD_MKDIR_ERROR:
	PSH R1
	CE R0 0x0001
	JMPC CMD_MKDIR_ERROR_1
	CE R0 0x0002
	JMPC CMD_MKDIR_ERROR_2
	CE R0 0x0003
	JMPC CMD_MKDIR_ERROR_3
	CE R0 0x0004
	JMPC CMD_MKDIR_ERROR_4
	LD R1 STR_MKDIR_ERROR

	PRINT_CMD_MKDIR_ERROR_EXIT:
		CALL PrintLineAtCursor
		POP R1
		RET

	CMD_MKDIR_ERROR_1:
		LD R1 STR_MKDIR_ERROR_1
		JMP PRINT_CMD_MKDIR_ERROR_EXIT
	CMD_MKDIR_ERROR_2:
		LD R1 STR_MKDIR_ERROR_2
		JMP PRINT_CMD_MKDIR_ERROR_EXIT
	CMD_MKDIR_ERROR_3:
		LD R1 STR_MKDIR_ERROR_3
		JMP PRINT_CMD_MKDIR_ERROR_EXIT
	CMD_MKDIR_ERROR_4:
		LD R1 STR_MKDIR_ERROR_4
		JMP PRINT_CMD_MKDIR_ERROR_EXIT

CMD_RMDIR:
	; R1 has pointer to 0 terminated string
	; Need to convert to an 12 char padded string
	SUB R0 5
	CLT R0 1
	JMPC CMD_RMDIR_NOARG
	CALL MakeAbsoluteDir
	CALL SplitDirectory
	PSH R2

	; PSH
	; CALL PrintLineAtCursor
	; POP

	; R1 has pointer to path
	; R2 has pointer to file name
	; Stack has pointer to file name to free it

	LD R0 $currentDisk ; Disk
	; LD R1 currentDirectory ; Ptr to path
	; R2 is pointer to file name
	CALL vfs_rmdir
	CNZ R0
	CALLC PRINT_CMD_RMDIR_ERROR

	POP R0
	CALL free
	RET

CMD_RMDIR_NOARG:
	LD R1 STR_RMDIR_NOARG
	CALL PrintLineAtCursor
	RET

PRINT_CMD_RMDIR_ERROR:
	PSH R1
	CE R0 0x0001
	JMPC CMD_RMDIR_ERROR_1
	CE R0 0x0002
	JMPC CMD_RMDIR_ERROR_2
	CE R0 0x0003
	JMPC CMD_RMDIR_ERROR_3
	CE R0 0x0004
	JMPC CMD_RMDIR_ERROR_4
	LD R1 STR_RMDIR_ERROR

	PRINT_CMD_RMDIR_ERROR_EXIT:
		CALL PrintLineAtCursor
		POP R1
		RET

	CMD_RMDIR_ERROR_1:
		LD R1 STR_RMDIR_ERROR_1
		JMP PRINT_CMD_RMDIR_ERROR_EXIT
	CMD_RMDIR_ERROR_2:
		LD R1 STR_RMDIR_ERROR_2
		JMP PRINT_CMD_RMDIR_ERROR_EXIT
	CMD_RMDIR_ERROR_3:
		LD R1 STR_RMDIR_ERROR_3
		JMP PRINT_CMD_RMDIR_ERROR_EXIT
	CMD_RMDIR_ERROR_4:
		LD R1 STR_RMDIR_ERROR_4
		JMP PRINT_CMD_RMDIR_ERROR_EXIT

CMD_HALT:
	LD R1 STR_CMD_HALT
	CALL PrintLineAtCursor
	HLT

CMD_CLEAR:
	ST 0 $Cursor
	; LD R2 CharMem
	LD R3 0x1000

	CMD_CLEAR_LOOP:
		LD R0 ' '
		CALL PrintAtCursor ; Clear display
		; DD R2
		; STD ' '
		; INC R2 ; Clear char mem
		DEC R3
		CNZ R3
		JMPC CMD_CLEAR_LOOP
	ST 0 $Cursor
	RET

; R1 -> Pointer to the absolute string
; R1 <- Pointer to path excluding name
; R2 <- Pointer to 12 character directory name
SplitDirectory:
	PSH R1
	; PSH
	; CALL PrintLineAtCursor
	; POP

	; R1 is the ptr to argument made absolute

	SplitDirectory_FindEnd:
		DD R1
		INC R1
		LDD R0
		; DBGC 'F'
		; DBGC R0
		CNZ R0
		JMPC SplitDirectory_FindEnd

	SUB R1 2
	DD R1
	LDD R0
	STD 0

	; R1 now has pointer to end of string
	SplitDirectory_FindLastSlash:
		DEC R1
		DD R1
		LDD R0
		; DBGC 'R'
		; DBGC R0
		CNE R0 '/'
		JMPC SplitDirectory_FindLastSlash

	INC R1
	PSH R1
	; R1 pointer right after last slash, to folder name in place
	; Stack has pointer to path
	CALL char12

	; PSH
	; CALL PrintLineAtCursor
	; POP

	; Now that we have the new folder name out of the way,
	; Write a zero after the middle slash
	; And use that for the path

	POP R3
	DD R3
	LDD R0
	STD 0


	PSH R1
	POP R2
	POP R1
	RET

; R1: pointer to zero terminated path string.
MakeAbsoluteDir:
	PSH R0
	PSH R2
	PSH R3
	
	DD R1
	LDD R0
	CE R0 '/'
	JMPC MakeAbsoluteDir_Almost_Done
	; R1 has pointer to relative string
	; Steps: Copy current into absolute, then copy relative onto absolute, then enforce trailing slash
	LD R2 currentDirectory
	LD R3 absoluteDirectory
	MakeAbsoluteDir_CopyLoop1:
		DD R2
		LDD R0
		DD R3
		STD R0
		INC R2
		INC R3
		CNZ R0
		JMPC MakeAbsoluteDir_CopyLoop1
	DEC R3
	; Now to copy the relative onto the absolute!
	MakeAbsoluteDir_CopyLoop2:
		DD R1
		LDD R0
		DD R3
		STD R0
		INC R1
		INC R3
		CNZ R0
		JMPC MakeAbsoluteDir_CopyLoop2
	; Enforce trailing slash
	DEC R3
	DD R3
	STD '/'
	INC R3
	DD R3
	STD 0

	MakeAbsoluteDir_Done:
	LD R1 absoluteDirectory
	POP R3
	POP R2
	POP R0
	RET

	MakeAbsoluteDir_Almost_Done:
	LD R2 absoluteDirectory
	MakeAbsoluteDir_Almost_Done_Loop:
		DD R1
		LDD R0
		DD R2
		STD R0
		; DBGC R0
		INC R1
		INC R2
		CNZ R0
		JMPC MakeAbsoluteDir_Almost_Done_Loop
	SUB R2 2
	DD R2
	LDD R0
	; DBGC R0
	CE R0 '/'
	JMPC MakeAbsoluteDir_Done ; Trailing slash? Done!
	INC R2
	DD R2
	STD '/'
	INCD
	STD 0
	JMP MakeAbsoluteDir_Done ; Now we've got a trailing slash!

; R1->ptr to 0 terminated string
; R1<-ptr to malloc'd 12 byte string
char12:
	PSH R0
	PSH R2
	PSH R3
	LD R0 12
	LD R3 12
	CALL malloc
	PSH R0
	; R0 has ptr to new buffer
	; R1 has point to old string
	char12_copyloop:
		DD R1
		LDD R2
		DD R0
		STD R2 ; Copy from one buffer to the other
		INC R0
		INC R1
		DEC R3
		CEZ R3
		JMPC char12_outofroom
		CNZ R2
		JMPC char12_copyloop
	; Now we've hit a zero, fill in the rest with zeroes
	char12_clearloop:
		DD R0
		STD 0 ; Copy from one buffer to the other
		INC R0
		DEC R3
		CNZ R3
		JMPC char12_clearloop

	char12_outofroom:
	POP R1
	POP R3
	POP R2
	POP R0
	RET

; Table Format: string0ptr0...string0ptr00
HUI_Table:
.ds "ls"
.db 0
.db CMD_LS
.db 0
.ds "halt"
.db 0
.db CMD_HALT
.db 0
.ds "exit"
.db 0
.db CMD_HALT
.db 0
.ds "mkdir"
.db 0
.db CMD_MKDIR
.db 0
.ds "rmdir"
.db 0
.db CMD_RMDIR
.db 0
.ds "clear"
.db 0
.db CMD_CLEAR
.db 0
.ds "cd"
.db 0
.db CMD_CD
.db 0
.ds "echo"
.db 0
.db CMD_ECHO
.db 0
.ds "pwd"
.db 0
.db CMD_PWD
.db 0
.db 0

; R0: Character
ApplyShift:
	PSH R1
	LD R1 $KB_Mods
	AND R1 0b0001
	CEZ R1
	POP R1
	RETC
	SUB R0 32
	ADD R0 ShiftTable
	DD R0
	LDD R0
	RET

; R0: State
; R1: Modifier Position
SetModifier:
	PSH R0
	LD R0 1
	SHL R1
	NOT R0
	AND R0 $KB_Mods ; Mask out the bit for the specific modifier
	STZ R2
	POP R0
	SHL R1
	ADD R2 ; Add masked to state
	ST R0 $KB_Mods
	; DBG R0
	RET

; R0: Character
; Destroys R1
SetAtCursor:
	ST R0 $0xaf70		; Character
	LD R1 $Cursor
	AND R1 0x003f
	ST R1 $0xaf71		; X Pos
	LD R1 $Cursor
	SHR R1 6
	ST R1 $0xaf72		; Y Pos
	ST Font $0xaf73		; Font Address
	INT 0x5f
	RET

; R0: Character
; Destroys R1
PrintAtCursor:
	LD R1 $Cursor		; Load the cursor position
	ADD R1 CharMem
	DD R1
	STD R0
	CALL SetAtCursor
	LD R1 $Cursor
	INC R1
	ST R1 $Cursor
	RET

; R1->ptr
PrintStringAtCursor:
	DD R1
	LDD R0
	CEZ R0
	JMPC PrintStringAtCursorEnd
		PSH R1
		CE R0 0x0a
		CALLC CursorNewline
		CNE R0 0x0a
		CALLC PrintAtCursor
	POP R1
	INC R1
	JMP PrintStringAtCursor
	PrintStringAtCursorEnd:
		RET

PrintLineAtCursor:
	CALL PrintStringAtCursor
	CALL CursorNewline
	RET

RefreshDisplay:
	ST CharMem $0xaf70
	ST 0 $0xaf71				;; Set the X position to 0
	ST 0 $0xaf72				;; Set the Y position to 0
	ST Font $0xaf73				;; Set the font to the font address
	INT 0x5e
	RET

Timer:				.db 0xffff
Cursor: 			.db 0x0001 ; (ypos, xpos)
LastCursor:			.db 0x0001 ; ^
CommandEnd:			.db 0x0001 ; ^
KB_Mods:			.db 0 ; 0000 0000 0000 win+alt+ctrl+shift

Font:
#INC ./fontGen/font.hex

ShiftTable:
.ds "       "    <_>?)!@#$%^&*( : +  "
.ds "                           {|}  "
.ds "~ABCDEFGHIJKLMNOPQRSTUVWXYZ     "

CharMem:
.ds ">"
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.ds "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz="
.db 0

RAMDISKSIZE: .db 0x1000
RAMDISK: .dz RAMDISKSIZE